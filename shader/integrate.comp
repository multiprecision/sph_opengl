#version 450
#extension GL_ARB_shading_language_420pack : enable

#define M_PI_F 3.14159265358979323846f
#define WORK_GROUP_SIZE 128

layout (local_size_x = WORK_GROUP_SIZE) in;

struct particle_type
{
    vec2 position;
    vec2 velocity;
    vec2 force;
    float density;
    float pressure;
};

layout(std430, binding = 0) buffer position_block
{
    particle_type particles[];
};

// constants
const float time_step = 0.0001f;
const float restitution_coefficient = 0.3f;
const uint particle_count = 20000;

void main()
{
    uint i = gl_GlobalInvocationID.x;

    // 4. integrate
    vec2 new_velocity = particles[i].velocity + time_step * particles[i].force / particles[i].density;
    vec2 new_position = particles[i].position + time_step * new_velocity;

    // 5. handle collision
    if (new_position.x < -1)
    {
        new_position.x = -1;
        new_velocity.x *= -1 * restitution_coefficient;
    }
    else if (new_position.x > 1)
    {
        new_position.x = 1;
        new_velocity.x *= -1 * restitution_coefficient;
    }
    else if (new_position.y < -1)
    {
        new_position.y = -1;
        new_velocity.y *= -1 * restitution_coefficient;
    }
    else if (new_position.y > 1)
    {
        new_position.y = 1;
        new_velocity.y *= -1 * restitution_coefficient;
    }
    // write the results
    particles[i].velocity = new_velocity;
    particles[i].position = new_position;
}
